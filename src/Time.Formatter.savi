:struct Time.Formatter
  :const month_names Array(String)'val: [
    "January"
    "February"
    "March"
    "April"
    "May"
    "June"
    "July"
    "August"
    "September"
    "October"
    "November"
    "December"
  ]

  :const day_names Array(String)'val: [
    "Sunday"
    "Monday"
    "Tuesday"
    "Wednesday"
    "Thursday"
    "Friday"
    "Saturday"
  ]

  :let pattern String'box
  :new (@pattern)

  :fun format(time Time'box, out String'ref = String.new)
    i USize = 0
    original_out_size = out.size
    is_bad_format = False

    while i < @pattern.size() (
      try (
        char U8 = @pattern.byte_at!(i)

        ok = True

        if char == '%' (
          char = @pattern.byte_at!(i + 1)
          case (
          | char == 'a' | @short_day_name(time, out)
          | char == 'A' | @day_name(time, out)
          | (char == 'b') || (char == 'h') | @short_month_name(time, out)
          | char == 'c' | @date_and_time(time, out)
          | char == 'B' | @month_name(time, out)
          | char == 'C' | @year_divided_by_100(time, out)
          | char == 'd' | @day_of_month_zero_padded(time, out)
          | (char == 'D') || (char == 'x') | @date(time, out)
          | char == 'e' | @day_of_month_blank_padded(time, out)
          | char == 'F' | @year_month_day(time, out)
          | char == 'g' | @calendar_week_year_modulo100(time, out)
          | char == 'G' | @calendar_week_year(time, out)
          | char == 'H' | @hour_24_zero_padded(time, out)
          | char == 'I' | @hour_12_zero_padded(time, out)
          | char == 'j' | @day_of_year_zero_padded(time, out)
          | char == 'k' | @hour_24_blank_padded(time, out)
          | char == 'l' | @hour_12_blank_padded(time, out)
          | char == 'L' | @milliseconds(time, out)
          | char == 'm' | @month_zero_padded(time, out)
          | char == 'M' | @minute(time, out)
          | char == 'N' | @second_fraction(time, out)
          | char == 'p' | out << @am_pm(time)
          | char == 'P' | out << @am_pm_upcase(time)
          | char == 'r' | @twelve_hour_time(time, out)
          | char == 'R' | @twenty_four_hour_time(time, out)
          | char == 's' | @unix_seconds(time, out)
          | char == 'S' | @second(time, out)
          | (char == 'T') || (char == 'X') | @twenty_four_hour_time_with_seconds(time, out)
          | char == 'u' | @day_of_week_monday_1_7(time, out)
          | char == 'V' | @calendar_week_week(time, out)
          | char == 'w' | @day_of_week_sunday_0_6(time, out)
          | char == 'y' | @year_modulo_100(time, out)
          | char == 'Y' | @year(time, out)
          | char == 'z' | @time_zone(time, False, out)
          | char == '_' |
            char = @pattern.byte_at!(i + 2)
            case (
            | char == 'm' | out << @month_blank_padded(time)
            |
              ok = False
              out.push_byte('%')
              out.push_byte('_')
            )
            if ok (i += 1)
          | char == '-' |
            char = @pattern.byte_at!(i + 2)
            case (
            | char == 'd' | out << @day_of_month(time)
            | char == 'm' | out << @month(time)
            |
              ok = False
              out.push_byte('%')
              out.push_byte('-')
            )
            if ok (i += 1)
          | char == '^' |
            char = @pattern.byte_at!(i + 2)
            case (
            | char == 'a' | out << @short_day_name_upcase(time)
            | char == 'A' | out << @day_name_upcase(time)
            | (char == 'b') || (char == 'h') | out << @short_month_name_upcase(time)
            | char == 'B' | out << @month_name_upcase(time)
            |
              ok = False
              out.push_byte('%')
              out.push_byte('^')
            )
            if ok (i += 1)
          | char == ':' |
            char = @pattern.byte_at!(i + 2)
            case (
            | char == 'z' | out << @time_zone_colon(time)
            | char == ':' |
              char = @pattern.byte_at!(i + 3)
              case (
              | char == 'z' | out << @time_zone_colon_with_seconds(time)
              |
                ok = False
                out.push_byte('%')
                out.push_byte(':')
                out.push_byte(':')
              )
              if ok (i += 1)
            |
              out.push_byte('%')
              out.push_byte(':')
            )
            if ok (i += 1)
          | (char == '3') || (char == '6') || (char == '9') |
            digit_char = char
            char = @pattern.byte_at!(i + 2)
            case (
            | char == 'N' |
              case (
              | digit_char == '3' | out << @milliseconds(time)
              | digit_char == '6' | out << @microseconds(time)
              | digit_char == '9' | out << @nanoseconds(time)
              )
            |
              ok = False
              out.push_byte('%')
              out.push_byte(digit_char)
            )
            if ok (i += 1)
          | char == '%' |
            out.push_byte('%')
          |
            out.push_byte('%')
            out.push_byte(char)
          )
          i += 1
        |
          out.push_byte(char)
        )
        i += 1
      |
        is_bad_format = True
      )
    )

    // If the format was bad, we undo all of the stuff we may have written
    // by trimming the output string to its original size before we started.
    if is_bad_format (
      out.trim_in_place(0, original_out_size)
    )

    out

  :fun date_and_time(time Time'box, out String'ref = String.new)
    out << @short_day_name(time)
    out.push_byte(' ')
    out << @short_month_name(time)
    out.push_byte(' ')
    out << @day_of_month_blank_padded(time)
    out.push_byte(' ')
    out << @twenty_four_hour_time_with_seconds(time)
    out.push_byte(' ')
    out << @year(time)
    out

  :fun date(time Time'box, out String'ref = String.new)
    out << @month_zero_padded(time)
    out.push_byte('/')
    out << @day_of_month_zero_padded(time)
    out.push_byte('/')
    out << @year_modulo_100(time)
    out

  :fun year_month_day(time Time'box, out String'ref = String.new)
    out << @year(time)
    out.push_byte('-')
    out << @month_zero_padded(time)
    out.push_byte('-')
    out << @day_of_month_zero_padded(time)
    out

  :fun twelve_hour_time(time Time'box, out String'ref = String.new)
    out << @hour_12_zero_padded(time)
    out.push_byte(':')
    out << @minute(time)
    out.push_byte(':')
    out << @second(time)
    out.push_byte(' ')
    out << @am_pm_upcase(time)
    out

  :fun twenty_four_hour_time(time Time'box, out String'ref = String.new)
    out << @hour_24_zero_padded(time)
    out.push_byte(':')
    out << @minute(time)
    out

  :fun twenty_four_hour_time_with_seconds(time Time'box, out String'ref = String.new)
    out << @hour_24_zero_padded(time)
    out.push_byte(':')
    out << @minute(time)
    out.push_byte(':')
    out << @second(time)
    out

  :fun year(time Time'box, out String'ref = String.new)
    @pad4(time.year.u64, '0', out)

  :fun year_modulo_100(time Time'box, out String'ref = String.new)
    @pad2((time.year % 100).u64, '0', out)

  :fun year_divided_by_100(time Time'box, out String'ref = String.new)
    Inspect.into(out, (time.year / 100).u32)
    out

  :fun full_or_short_year(time Time'box, out String'ref = String.new)
    @year(time, out)

  :fun calendar_week_year(time Time'box, out String'ref = String.new)
    try (
      calendar_week_0 = time.calendar_week()[0]!
      @pad4(calendar_week_0, '0', out)
    )
    out

  :fun calendar_week_year_modulo100(time Time'box, out String'ref = String.new)
    try (
      calendar_week_0 = time.calendar_week()[0]!
      @pad2(calendar_week_0 % 100, '0', out)
    )
    out

  :fun month(time Time'box, out String'ref = String.new)
    Inspect.into(out, time.month)
    out

  :fun month_zero_padded(time Time'box, out String'ref = String.new)
    @pad2(time.month.u64, '0', out)

  :fun month_blank_padded(time Time'box, out String'ref = String.new)
    @pad2(time.month.u64, ' ', out)

  :fun month_name(time Time'box, out String'ref = String.new)
    out << @get_month_name(time)
    out

  :fun month_name_upcase(time Time'box, out String'ref = String.new)
    // TODO
    // get_month_name.upcase
    out

  :fun short_month_name(time Time'box, out String'ref = String.new)
    out << @get_short_month_name(time)
    out

  :fun short_month_name_upcase(time Time'box, out String'ref = String.new)
    // TODO
    // get_short_month_name.upcase
    out

  :fun calendar_week_week(time Time'box, out String'ref = String.new)
    try (
      calendar_week_1 = time.calendar_week()[1]!
      @pad2(calendar_week_1, '0', out)
    )
    out

  :fun day_of_month(time Time'box, out String'ref = String.new)
    Inspect.into(out, time.day)
    out

  :fun day_of_month_zero_padded(time Time'box, out String'ref = String.new)
    @pad2(time.day.u64, '0', out)

  :fun day_of_month_blank_padded(time Time'box, out String'ref = String.new)
    @pad2(time.day.u64, ' ', out)

  :fun day_name(time Time'box, out String'ref = String.new)
    out << @get_day_name(time)
    out

  :fun day_name_upcase(time Time'box, out String'ref = String.new)
    // TODO
    // @get_day_name(time).upcase
    ""

  :fun short_day_name(time Time'box, out String'ref = String.new)
    out << @get_short_day_name(time)
    out

  :fun short_day_name_upcase(time Time'box, out String'ref = String.new)
    // TODO
    // get_short_day_name.upcase
    ""

  :fun short_day_name_with_comma(time Time'box, out String'ref = String.new)
    out << @short_day_name(time)
    out.push_byte(',')
    out.push_byte(' ')
    out

  :fun day_of_year_zero_padded(time Time'box, out String'ref = String.new)
    @pad3(time.day_of_year, '0', out)

  :fun hour_24_zero_padded(time Time'box, out String'ref = String.new)
    @pad2(time.hour.u64, '0', out)

  :fun hour_24_blank_padded(time Time'box, out String'ref = String.new)
    @pad2(time.hour.u64, ' ', out)

  :fun hour_12_zero_padded(time Time'box, out String'ref = String.new)
    h = (time.hour % 12).u64
    @pad2(if h == 0 (12 | h), '0', out)

  :fun hour_12_blank_padded(time Time'box, out String'ref = String.new)
    h = (time.hour % 12).u64
    @pad2(if h == 0 (12 | h), ' ', out)

  :fun minute(time Time'box, out String'ref = String.new)
    @pad2(time.minute.u64, '0', out)

  :fun second(time Time'box, out String'ref = String.new)
    @pad2(time.second.u64, '0', out)

  :fun milliseconds(time Time'box, out String'ref = String.new)
    @pad3((time.nanosecond / 1000000).u64, '0', out)

  :fun microseconds(time Time'box, out String'ref = String.new)
    @pad6((time.nanosecond / 1000).u64, '0', out)

  :fun nanoseconds(time Time'box, out String'ref = String.new)
    @pad9(time.nanosecond.u64, '0', out)

  :fun second_fraction(time Time'box, out String'ref = String.new)
    @nanoseconds(time, out)

  :fun second_fraction!(
    time Time'box
    fraction_digits I32 = 9
    out String'ref = String.new
  )
    case (
    | fraction_digits == 0 |
    | fraction_digits == 3 |
      out.push_byte('.')
      @milliseconds(time, out)
    | fraction_digits == 6 |
      out.push_byte('.')
      @microseconds(time, out)
    | fraction_digits == 9 |
      out.push_byte('.')
      @nanoseconds(time, out)
    |
      error!
    )
    out

  :fun am_pm(time Time'box) String
    if time.hour < 12 ("am" | "pm")

  :fun am_pm_upcase(time Time'box) String
    if time.hour < 12 ("AM" | "PM")

  :fun day_of_week_monday_1_7(time Time'box, out String'ref = String.new)
    Inspect.into(out, time.day_of_week.u64)
    out

  :fun day_of_week_sunday_0_6(time Time'box, out String'ref = String.new)
    Inspect.into(out, time.day_of_week.u64 % 7)
    out

  :fun unix_seconds(time Time'box, out String'ref = String.new)
    Inspect.into(out, time.to_unix)
    out

  :fun time_zone(time Time'box, with_seconds = False, out String'ref = String.new)
    // TODO
    // time_zone_offset(format_seconds: with_seconds)
    out

  // def time_zone_z_or_offset(**options)
  //   if time.utc?
  //     io << 'Z'
  //   else
  //     time_zone_offset(**options)
  //   end
  // end

  // def time_zone_offset(force_colon = false, allow_colon = true, format_seconds = false, parse_seconds = true)
  //   time.zone.format(io, with_colon: force_colon, with_seconds: format_seconds)
  // end

  :fun time_zone_colon(time Time'box, with_seconds = False, out String'ref = String.new)
    // TODO
    // time_zone_offset(force_colon: true, format_seconds: with_seconds)
    out

  :fun time_zone_colon_with_seconds(time Time'box, out String'ref = String.new)
    @time_zone_colon(time, True, out)

  :fun time_zone_gmt(time Time'box, out String'ref = String.new)
    out << "GMT"
    out

  :fun time_zone_rfc2822(time Time'box, out String'ref = String.new)
    // TODO
    // time_zone_offset(allow_colon: false)
    out

  // def time_zone_gmt_or_rfc2822(**options)
  //   if time.utc? || time.location.name == "UT" || time.location.name == "GMT"
  //     time_zone_gmt
  //   else
  //     time_zone_rfc2822
  //   end
  // end

  :fun get_month_name(time Time'box) String
    try (@month_names[time.month.usize - 1]! | "")

  :fun get_short_month_name(time Time'box) String
    @get_month_name(time).substring(0, 3)

  :fun get_day_name(time Time'box) String
    try (@day_names[time.day_of_week.usize % 7]! | "")

  :fun get_short_day_name(time Time'box) String
    @get_day_name(time).substring(0, 3)

  :fun pad2(value U64, padding U8, out String'ref = String.new)
    if value < 10 out.push_byte(padding)
    Inspect.into(out, value)
    out

  :fun pad3(value U64, padding U8, out String'ref = String.new)
    if value < 100 out.push_byte(padding)
    @pad2(value, padding, out)

  :fun pad4(value U64, padding U8, out String'ref = String.new)
    if value < 1000 out.push_byte(padding)
    @pad3(value, padding, out)

  :fun pad6(value U64, padding U8, out String'ref = String.new)
    if value < 100000 out.push_byte(padding)
    if value < 10000 out.push_byte(padding)
    @pad4(value, padding, out)

  :fun pad9(value U64, padding U8, out String'ref = String.new)
    if value < 100000000 out.push_byte(padding)
    if value < 10000000 out.push_byte(padding)
    if value < 1000000 out.push_byte(padding)
    @pad6(value, padding, out)
